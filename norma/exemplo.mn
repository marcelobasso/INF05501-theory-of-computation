// Programa de exemplo do simulador de Máquina Norma

// Comentários de linha começam com '//'

// Registradores são strings começando com 
// letras maiúsculas. Ex: X,Y,A,B,Abc,A1,B_2 ...

// Rótulos são strings começando com 
// números ou minúsculas. Ex: 1, 1a, ab1, a_2 ...

// Comandos disponíveis 
// inc R              (R:=R+1)
// dec R              (R:=R-1)
// add R n            (R:=R+n)
// sub R n            (R:=R-n)
// foo(R1,R2,...,Rn)  chamada de subrotina de operação

// Testes disponíveis 
// zero R             (R=0)
// cmp  R n           (R=n)
// foo(R1,R2,...,Rn)  chamada de subrotina de teste

// Definição de subrotina de operação:
// Rótulo inicial = rótulo da primeira instrução       (1 por convenção)
// Rótulo final   = referência a rótulo que não ocorre (0 por convenção)
// Parâmetros     = nomes de registradores, separados por vírgula

// Exemplo: A:=A+B usando C
operation soma(A,B,C){
  1: if zero B then goto 5 else goto 2
  2: do dec  B goto 3
  3: do inc  A goto 4
  4: do inc  C goto 1
  5: if zero C then goto 0 else goto 6
  6: do dec  C goto 7
  7: do inc  B goto 5
}

// Definição de subrotina de teste:
// Rótulo inicial = rótulo da primeira instrução       (1 por convenção)
// Rótulo final   = true ou false, obrigatoriamente
// Parâmetros     = nomes de registradores, separados por vírgula

// testa se A e B contém ambos o valor zero
test ambosZero(A,B) {
  1: if zero A then goto 2    else goto false
  2: if zero B then goto true else goto false
}

// O programa principal começa com o identificador 'main'
// Exemplo:

main{
 1: do add A 3 goto 2
 2: do soma(Y,X,B) goto 3
 3: do soma(Y,A,B) goto 4
 4: if ambosZero(X,Y) then goto 0 else goto 5
 5: do inc Y goto 0
}
